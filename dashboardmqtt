#include <WiFi.h>
#include <MQTT.h>

const char ssid[] = "SSID";
const char pass[] = "WIFI_PASSWORD";

const char mqtt_broker[] = "test.mosquitto.org";
const char mqtt_topic[]  = "group/led";
const char mqtt_client_id[] = "arduino_group_led_xxx"; // ต้องไม่ซ้ำ
int MQTT_PORT = 1883;

#define LED_PIN 2

WiFiClient net;
MQTTClient client;

void connect() {
  Serial.print("checking wifi...");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(1000);
  }

  Serial.print("\nconnecting mqtt...");
  while (!client.connect(mqtt_client_id)) {
    Serial.print(".");
    delay(1000);
  }

  Serial.println("\nconnected!");

  client.subscribe(mqtt_topic);
}

void messageReceived(String &topic, String &payload) {
  Serial.println("incoming: " + topic + " - " + payload);

  if (payload == "on") {
    digitalWrite(LED_PIN, HIGH);
    Serial.println("LED ON");
  }
  else if (payload == "off") {
    digitalWrite(LED_PIN, LOW);
    Serial.println("LED OFF");
  }
}

void setup() {
  Serial.begin(9600);
  WiFi.begin(ssid, pass);

  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, LOW);

  client.begin(mqtt_broker, MQTT_PORT, net);
  client.onMessage(messageReceived);

  connect();
}

void loop() {
  client.loop();
  delay(10);

  if (!client.connected()) {
    connect();
  }
}
